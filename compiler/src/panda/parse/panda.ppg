// Assumes polyglot.jar is in classpath
include "polyglot/ext/jl7/parse/jl7.ppg"

package panda.parse;

import panda.types.PandaTypeSystem;
import panda.ast.PandaNodeFactory;
import panda.ast.*;
import polyglot.parse.Name;
import polyglot.parse.VarDeclarator;

parser Grm extends polyglot.ext.jl7.parse.Grm {:
  public final PandaTypeSystem ts;
  public final PandaNodeFactory nf;

  public Grm(Lexer l, TypeSystem t, NodeFactory n, ErrorQueue q) {
      super(l, t, n, q);
      ts = (PandaTypeSystem) t;
      nf = (PandaNodeFactory) n;
  } 
  
:};

terminal Token MODE; 
terminal Token MODES;
terminal Token PARORD;

non terminal ModesDecl modes_declaration_opt;
non terminal ModesDecl modes_declaration;
non terminal ModeOrder mode_ordering;
non terminal List mode_orderings;

non terminal List mode_type_parameters_decl_opt;
non terminal List mode_type_parameters_decl;
non terminal List mode_type_parameters;
non terminal ModeParamTypeNode mode_type_parameter;

non terminal ModeTypeNode mode_type_decl;
non terminal List<ModeValueNode> mode_type_argument_list;
non terminal List<ModeValueNode> mode_type_arguments;
non terminal ModeTypeNode mode_type;
non terminal ModeValueNode mode_value;

start with goal;

override compilation_unit ::=
                    // SourceFile
  package_declaration_opt:p import_declarations_opt:i modes_declaration_opt:m type_declarations_opt:t 
  {: 
    RESULT = parser.nf.SourceFile(new Position(parser.lexer.path(), 
                                               parser.lexer.file()), 
                                  p, 
                                  i, 
                                  t,
                                  m);
  :}
| error type_declarations_opt:c
  {: RESULT = parser.nf.SourceFile(new Position(parser.lexer.path(), 
                                                parser.lexer.file()), 
                                   null, 
                                   Collections.<Import> emptyList(), 
                                   c, 
                                   null);
  :}
;

modes_declaration_opt ::=
  modes_declaration:m
  {: RESULT = m; :}
|
  {: RESULT = null; :}
;

modes_declaration ::=
  MODES LBRACE:lb mode_orderings:ords RBRACE:rb
  {:
    RESULT = parser.nf.ModesDecl(parser.pos(lb, rb), ords);
  :}
;

mode_orderings ::=
  {: RESULT = new ArrayList(); :}
| mode_orderings:ords mode_ordering:m
  {: 
    RESULT = ords;
    ords.add(m);
  :}
;
  
mode_ordering ::=
  IDENTIFIER:lower PARORD IDENTIFIER:upper SEMICOLON:semi
  {: RESULT = 
    parser.nf.ModeOrder(parser.pos(lower, semi), 
                        lower.getIdentifier(), 
                        upper.getIdentifier()); 
  :}
;

override class_declaration ::=
  modifiers_or_annotations_opt:a CLASS:n IDENTIFIER:b type_parameters_opt:c mode_type_parameters_decl_opt:m super_opt:d interfaces_opt:e class_body:f
  {: 
    RESULT = parser.nf.ClassDecl(parser.pos(n, e),  
                                a.flags(), 
                                a.annotations(), 
                                parser.nf.Id(parser.pos(b), b.getIdentifier()), 
                                d, 
                                e, 
                                f, 
                                c,
                                m); 
  :}
;    

mode_type_parameters_decl_opt ::=
  mode_type_parameters_decl:m
  {: RESULT = m; :}
| /* empty */
  {: RESULT = null; :}
;

mode_type_parameters_decl ::=
  AT:a MODE LT mode_type_parameters:params GT
  {: RESULT = params; :}
;

mode_type_parameters ::=
  mode_type_parameter:m
  {: 
    List<ModeParamTypeNode> params = new ArrayList<ModeParamTypeNode>();
    params.add(m);
    RESULT = params;
  :}
| mode_type_parameters:params COMMA mode_type_parameter:m
  {: 
    RESULT = params;
    params.add(m);
  :}
;

mode_type_parameter ::=
  IDENTIFIER:id
  {: 
    RESULT = 
      parser.nf.ModeParamTypeNode(parser.pos(id), 
                                  parser.nf.Id(parser.pos(id), id.getIdentifier()));
  :}
;

override type ::=
  primitive_type:a
  {: 
    ModeTypeNode m = parser.nf.ModeTypeNode(a.position()); 
    m.base(a);
    RESULT = m;
  :}
|  
  primitive_type:a mode_type_decl:m
  {: 
    m.base(a);
    RESULT = m;
  :}
|
  primitive_type:a mode_type_decl:m mode_type_argument_list:args
  {: 
    m.base(a);
    RESULT = parser.nf.AmbModeTypeInstantiation(m.position(), m, args);
  :}
|
  reference_type:a
  {: 
    ModeTypeNode m = parser.nf.ModeTypeNode(a.position());
    m.base(a);
    RESULT = m; 
  :}
|
  reference_type:a mode_type_decl:m
  {: 
    m.base(a);
    RESULT = m;
  :}
|
  reference_type:a mode_type_decl:m mode_type_argument_list:args
  {: 
    m.base(a);
    RESULT = parser.nf.AmbModeTypeInstantiation(m.position(), m, args);
  :}
;

mode_type_decl ::=
  AT:a MODE LPAREN mode_type:m RPAREN
  {:
    RESULT = m;
  :}
;

mode_type ::=
  IDENTIFIER:id
  {: 
    RESULT = parser.nf.ModeTypeNode(parser.pos(id),
                                    parser.nf.Id(parser.pos(id), 
                                                 id.getIdentifier()));
  :}
; 

mode_value ::=
  IDENTIFIER:id
  {:
    RESULT = parser.nf.ModeValueNode(parser.pos(id), id.getIdentifier());
  :}
;

mode_type_argument_list ::=
  LT mode_type_arguments:args GT
  {:
    RESULT = args;
  :}
;

mode_type_arguments ::=
  mode_value:m
  {:
    List<ModeValueNode> args = new ArrayList<ModeValueNode>();
    args.add(m);
    RESULT = args;
  :}
|
  mode_type_arguments:args COMMA mode_value:m
  {:
    RESULT = args;
    args.add(m);
  :}
; 
